package analizadores; 
import java_cup.runtime.*;
import Estructuras.Node;
import Estructuras.Arbol;
import Estructuras.Production;
import Estructuras.Instructions.Declaracion;
import Estructuras.Instructions.Operacion;
import Estructuras.Instructions.Instruccion;
import Estructuras.Instructions.Identacion;
import Estructuras.Instructions.If;

import java.util.LinkedList;

parser code 
{:
    /**
     * Variable en la que se almacena el arbol de sintaxis abstracta que se 
     * genera luego del analisis sintáctico.
     */
    public LinkedList<Instruccion> AST;
    public Arbol arbol;
    int iden;
    /**
     * Método al que se llama automáticamente ante algún error sintactico.
     **/ 
    public void syntax_error(Symbol s){ 
        System.out.println("Error Sintáctico en la Línea " + (s.left) +
        " Columna "+s.right+ ". No se esperaba este componente: " +s.value+"."); 
    } 

    /**
     * Método al que se llama automáticamente ante algún error sintáctico 
     * en el que ya no es posible una recuperación de errores.
     **/ 
    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception{ 
        System.out.println("Error síntactico irrecuperable en la Línea " + 
        (s.left)+ " Columna "+s.right+". Componente " + s.value + 
        " no reconocido."); 
    }  
        
    public Arbol getArbol() {
        return arbol;
    }

    public LinkedList<Instruccion> getAST() {
        return AST;
    }
    
    public void masIden(){
        iden++;
    }
    public void menosIden(){
        iden--;
    }

    public int getIden(){
        return this.iden;
    }

:} 


terminal String T_INGRESAR;
terminal String T_ID;
terminal String T_COMO;
terminal String T_STRING;
terminal String T_INT;
terminal String T_CADENA;
terminal String T_CON_VALOR;
terminal String T_ENTERO;
terminal String T_CARACTER;
terminal String T_CHAR;
terminal String T_BOOLEAN;
terminal String T_VALOR_BOOL;
terminal String T_FLOAT;
terminal String T_INICIO;
terminal String T_FIN;
terminal String T_ASIG;
terminal String PTCOMA,PARIZQ,PARDER,LLAVIZQ,LLAVDER;
terminal String MAS,MENOS,POR,DIVIDIDO;
terminal String MENQUE,MAYQUE;
terminal String T_SI;
terminal String UMENOS;
terminal String T_CONTRARIO;
terminal String T_O_SI;
terminal String T_ENTONCES;
terminal String T_FIN_SI;
terminal String T_MAYIG,T_MNIG,T_IGUAL,T_DIFE;




non terminal ini;
non terminal Production instrucciones;
non terminal Production instruccion;
non terminal Production declaracion;
non terminal Production expresion;
non terminal Production expresion_relacional;
non terminal Production else_ifs;
non terminal Production if;

precedence left MAS,MENOS;
precedence left POR,DIVIDIDO;
precedence right UMENOS; 


start with ini; 

ini::=T_INICIO instrucciones:a T_FIN  {:
                                parser.AST=a.getInstructions();
                                Node NODERAIZ = new Node("INIT"); NODERAIZ.addSon(a.getNode());
                                parser.arbol=new Arbol(NODERAIZ);
                                //menosIden();
                                
                                
                        :}



;

instrucciones ::= 
   instrucciones:a instruccion:b    {:
                                        RESULT=new Production(new Node("Instrucciones"), a.getInstructions());
                                        RESULT.addIns(b.getIns());
                                        RESULT.addSon(a);
                                        RESULT.addSon(b);
                                        
                                        //masIden();
                                        System.out.println("ESTOY ACA INSTRUCCIONES INSTRUCCION"+getIden());
                                        
                                    :}
 | instruccion :a       {:
                            RESULT=new Production(new Node("Instrucciones"), new LinkedList<>());
                            RESULT.addIns(a.getIns());
                            RESULT.addSon(a);
                            /*if(getIden()==0){
                                System.out.println("ESTOY ACAAAAAAAAAAAAAAAAAAAAA");
                            }else{
                                menosIden();
                            }*/
                            
                            
                            
                        :}

 
; 

instruccion ::= 
    declaracion:a         {:
                            RESULT=a;

                        :}
    |if:a         {:
                            RESULT=a;
                            menosIden();
                            System.out.println("ESTOY IF "+getIden());

                        :}
    
;

declaracion ::=
    T_INGRESAR T_ID:a T_COMO T_STRING T_CON_VALOR T_CADENA:b PTCOMA    {: 
                                                                RESULT = new Production(new Node("Declaracion"),  new Declaracion(a,b,"string",getIden()));
                                                                //masIden();
                                                                RESULT.addSon("ingresar");
                                                                RESULT.addSon(a);
                                                                RESULT.addSon("como");
                                                                RESULT.addSon("cadena");
                                                                RESULT.addSon("con_valor");
                                                                RESULT.addSon(b.replace("\"", "")); 
                                                            :}

    |T_INGRESAR T_ID:a T_COMO T_INT T_CON_VALOR expresion:b  PTCOMA   {: 
                                                                RESULT = new Production(new Node("Declaracion"),  new Declaracion(a,(Operacion)b.getIns(),"int",getIden()));
                                                                RESULT.addSon("ingresar");
                                                                RESULT.addSon(a);
                                                                RESULT.addSon("como");
                                                                RESULT.addSon("numero");
                                                                RESULT.addSon("con_valor");
                                                                RESULT.addSon(b); 
                                                                RESULT.addSon(";");
                                                            :}

    |T_INGRESAR T_ID:a T_COMO T_CARACTER T_CON_VALOR T_CHAR:b  PTCOMA   {: 
                                                                RESULT = new Production(new Node("Declaracion"),  new Declaracion(a,b,"char",getIden()));
                                                                RESULT.addSon("ingresar");
                                                                RESULT.addSon(a);
                                                                RESULT.addSon("como");
                                                                RESULT.addSon("caracter");
                                                                RESULT.addSon("con_valor");
                                                                RESULT.addSon(b); 
                                                                RESULT.addSon(";");
                                                            :}

    |T_INGRESAR T_ID:a T_COMO T_BOOLEAN T_CON_VALOR T_VALOR_BOOL:b  PTCOMA   {: 
                                                                RESULT = new Production(new Node("Declaracion"),  new Declaracion(a,b,"bool",getIden()));
                                                                RESULT.addSon("ingresar");
                                                                RESULT.addSon(a);
                                                                RESULT.addSon("como");
                                                                RESULT.addSon("boolean");
                                                                RESULT.addSon("con_valor");
                                                                RESULT.addSon(b); 
                                                                RESULT.addSon(";");
                                                            :}
                                                           
    
                                                            
                                                           
    |T_ID:a T_ASIG T_CADENA:b  PTCOMA   {: 
                                                                RESULT = new Production(new Node("Declaracion"),  new Declaracion(a,b,"asig",getIden()));
                                                                RESULT.addSon(a);
                                                                RESULT.addSon("->");
                                                                RESULT.addSon(b.replace("\"", "")); 
                                                                RESULT.addSon(";");
                                                            :}

                                                            
                                                           
    |T_ID:a T_ASIG expresion:b  PTCOMA   {: 
                                                                RESULT = new Production(new Node("Declaracion"),  new Declaracion(a,(Operacion)b.getIns(),"asig",getIden()));
                                                                RESULT.addSon(a);
                                                                RESULT.addSon("->");
                                                                RESULT.addSon(b); 
                                                                RESULT.addSon(";");
                                                            :}

                                                            
                                                           
    |T_ID:a T_ASIG T_CHAR:b  PTCOMA   {: 
                                                                RESULT = new Production(new Node("Declaracion"),  new Declaracion(a,b,"asig",getIden()));
                                                                RESULT.addSon(a);
                                                                RESULT.addSon("->");
                                                                RESULT.addSon(b); 
                                                                RESULT.addSon(";");
                                                            :}

                                                            
                                                           
    |T_ID:a T_ASIG T_VALOR_BOOL:b  PTCOMA   {: 
                                                                RESULT = new Production(new Node("Declaracion"),  new Declaracion(a,b,"asig",getIden()));
                                                                RESULT.addSon(a);
                                                                RESULT.addSon("->");
                                                                RESULT.addSon(b); 
                                                                RESULT.addSon(";");
                                                            :}
                                                           
                                                             
                                                                
;

if ::=

 T_SI expresion_relacional:a   instrucciones:b T_FIN_SI                                                     {:
                                                                                                                                RESULT=new Production(new Node("Instruccion"), new If((Operacion)a.getIns(), b.getInstructions(),getIden()));
                                                                                                                                //masIden();
                                                                                                                                RESULT.addSon("si");
                                                                                                                                
                                                                                                                                RESULT.addSon(a);
                                                                                                                                
                                                                                                                                
                                                                                                                                RESULT.addSon(b);
                                                                                                                                RESULT.addSon("fin_si");
                                                                                                                                
                                                                                                                                
                                                                                                                            :} 
 | T_SI  expresion_relacional:a  instrucciones:b  else_ifs:l T_CONTRARIO instrucciones:c T_FIN_SI    {:  
                                                                                                                                RESULT=new Production(new Node("Instruccion"), new If((Operacion)a.getIns(),b.getInstructions(),l.getInstructions(),c.getInstructions(),getIden()));
                                                                                                                                RESULT.addSon("si");
                                                                                                                                
                                                                                                                                RESULT.addSon(a);
                                                                                                                              
                                                                                                                                
                                                                                                                                RESULT.addSon(b);
                                                                                                                                
                                                                                                                                RESULT.addSon(l);
                                                                                                                                RESULT.addSon("de_lo_contrario");
                                                                                                                                
                                                                                                                                RESULT.addSon(c);
                                                                                                                                RESULT.addSon("fin_si");
                                                                                                                                //masIden();
                                                                                                                            :}



 | T_SI  expresion_relacional:a   instrucciones:b   T_CONTRARIO  instrucciones:c T_FIN_SI    {:  
                                                                                                                                RESULT=new Production(new Node("Instruccion"), new If((Operacion)a.getIns(),b.getInstructions(),c.getInstructions(),getIden()));
                                                                                                                                RESULT.addSon("si");
                                                                                                                                
                                                                                                                                RESULT.addSon(a);
                                                                                                                                
                                                                                                                                
                                                                                                                                RESULT.addSon(b);
                                                                                                                                
                                                                                                                                RESULT.addSon("de_lo_contrario");
                                                                                                                                
                                                                                                                                RESULT.addSon(c);
                                                                                                                                RESULT.addSon("fin_si");
                                                                                                                                //masIden();
                                                                                                                            :}



;


else_ifs ::= 
    else_ifs:l T_O_SI expresion_relacional:a  T_ENTONCES instrucciones:b                                 {:                                                                                                                                
                                                                                                                                RESULT=new Production(new Node("ELSE_IFS"), l.getInstructions());
                                                                                                                                RESULT.addIns(new If((Operacion)a.getIns(), b.getInstructions(),getIden()));
                                                                                                                                RESULT.addSon(l);
                                                                                                                                RESULT.addSon("o_si");
                                                                                                                                
                                                                                                                                RESULT.addSon(a);
                                                                                                                                
                                                                                                                                RESULT.addSon("entonces");
                                                                                                                                RESULT.addSon(b);
                                                                                                                                
                                                                                                                            :}
  | T_O_SI  expresion_relacional:a T_ENTONCES instrucciones:b                                              {:  
                                                                                                                                RESULT=new Production(new Node("ELSE_IFS"), new LinkedList<>());
                                                                                                                                RESULT.addIns(new If((Operacion)a.getIns(), b.getInstructions(),getIden()));
                                                                                                                                RESULT.addSon("o_si");
                                                                                                                                
                                                                                                                                RESULT.addSon(a);
                                                                                                                                RESULT.addSon("entonces");
                                                                                                                                RESULT.addSon(b);
                                                                                                                                
                                                                                                                            :}
;



expresion  ::= 
    MENOS expresion:a                           {:
                                                    RESULT=new Production(new Node("Expresion"), new Operacion((Operacion)a.getIns(),Operacion.Tipo_operacion.NEGATIVO));
                                                    RESULT.addSon("-");
                                                    RESULT.addSon(a);                                                
                                                :}%prec UMENOS     
 |  expresion:a MAS        expresion:b          {:
                                                    RESULT=new Production(new Node("Expresion"), new Operacion((Operacion)a.getIns(),(Operacion)b.getIns(),Operacion.Tipo_operacion.SUMA));
                                                    RESULT.addSon(a);
                                                    RESULT.addSon("+");
                                                    RESULT.addSon(b);
                                                :}
 |  expresion:a MENOS      expresion:b          {:
                                                    RESULT=new Production(new Node("Expresion"), new Operacion((Operacion)a.getIns(),(Operacion)b.getIns(),Operacion.Tipo_operacion.RESTA));
                                                    RESULT.addSon(a);
                                                    RESULT.addSon("-");
                                                    RESULT.addSon(b);
                                                :}
 |  expresion:a POR        expresion:b          {:
                                                    RESULT=new Production(new Node("Expresion"), new Operacion((Operacion)a.getIns(),(Operacion)b.getIns(),Operacion.Tipo_operacion.MULTIPLICACION));
                                                    RESULT.addSon(a);
                                                    RESULT.addSon("*");
                                                    RESULT.addSon(b);
                                                :}
 |  expresion:a DIVIDIDO   expresion:b          {:                                                
                                                    RESULT=new Production(new Node("Expresion"), new Operacion((Operacion)a.getIns(),(Operacion)b.getIns(),Operacion.Tipo_operacion.DIVISION));
                                                    RESULT.addSon(a);
                                                    RESULT.addSon("/");
                                                    RESULT.addSon(b);
                                                :}
 |  T_ENTERO:a                                    {:
                                                    RESULT=new Production(new Node(null, a), new Operacion(a,Operacion.Tipo_operacion.NUMERO));
                                                :}
 |  T_FLOAT:a                                   {:
                                                    RESULT=new Production(new Node(null, a), new Operacion(a,Operacion.Tipo_operacion.NUMERO));
                                                :}
 |  PARIZQ expresion:a PARDER                   {:
                                                    RESULT=new Production(new Node("Expresion"), a.getIns());
                                                    RESULT.addSon("(");
                                                    RESULT.addSon(a);
                                                    RESULT.addSon(")");
                                                :}
;

expresion_relacional  ::= 
   T_ID:a  MAYQUE   expresion:b            {:
                                                    RESULT=new Production(new Node("Expresion_Relacional"), new Operacion(a,(Operacion)b.getIns(), Operacion.Tipo_operacion.MAYOR_QUE));
                                                    RESULT.addSon(a);
                                                    RESULT.addSon(">");
                                                    RESULT.addSon(b);
                                                    masIden();
                                                :} 
 | T_ID:a  MENQUE   expresion:b            {:
                                                    RESULT=new Production(new Node("Expresion_Relacional"), new Operacion(a,(Operacion)b.getIns(), Operacion.Tipo_operacion.MENOR_QUE));
                                                    RESULT.addSon(a);
                                                    RESULT.addSon("<");
                                                    RESULT.addSon(b);
                                                    masIden();
                                                :}  
| T_ID:a  T_MAYIG   expresion:b            {:
                                                    RESULT=new Production(new Node("Expresion_Relacional"), new Operacion(a,(Operacion)b.getIns(), Operacion.Tipo_operacion.MAYIG));
                                                    RESULT.addSon(a);
                                                    RESULT.addSon(">=");
                                                    RESULT.addSon(b);
                                                    masIden();
                                                :} 

| T_ID:a  T_MNIG   expresion:b            {:
                                                    RESULT=new Production(new Node("Expresion_Relacional"), new Operacion(a,(Operacion)b.getIns(), Operacion.Tipo_operacion.MNIG));
                                                    RESULT.addSon(a);
                                                    RESULT.addSon("<=");
                                                    RESULT.addSon(b);
                                                    masIden();
                                                :} 
| T_ID:a  T_IGUAL   expresion:b            {:
                                                    RESULT=new Production(new Node("Expresion_Relacional"), new Operacion(a,(Operacion)b.getIns(), Operacion.Tipo_operacion.ES_IGUAL));
                                                    RESULT.addSon(a);
                                                    RESULT.addSon("=");
                                                    RESULT.addSon(b);
                                                    masIden();
                                                :} 

| T_ID:a  T_DIFE   expresion:b            {:
                                                    RESULT=new Production(new Node("Expresion_Relacional"), new Operacion(a,(Operacion)b.getIns(), Operacion.Tipo_operacion.ES_DIFERENTE));
                                                    RESULT.addSon(a);
                                                    RESULT.addSon("!=");
                                                    RESULT.addSon(b);
                                                    masIden();
                                                :} 
;